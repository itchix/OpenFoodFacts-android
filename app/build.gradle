apply plugin: 'com.android.application'
apply plugin: 'de.timfreiheit.resourceplaceholders.plugin'
apply plugin: 'org.greenrobot.greendao'

greendao {
    schemaVersion 12
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 311
        versionName "3.1.1"

        multiDexEnabled true
        // jackOptions.enabled = true
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "versionCode"
    productFlavors {
        off {
            applicationId "openfoodfacts.github.scrachx.openfood"
            resValue "string", "app_name", "OpenFoodFacts"
            buildConfigField 'String', 'HOST', '"https://ssl-api.openfoodfacts.org"'
            buildConfigField 'String', 'OFOTHERLINKAPP', '"org.openbeautyfacts.scanner"'
            buildConfigField 'String', 'OFWEBSITE', '"https://world.openfoodfacts.org/"'
            buildConfigField 'String', 'WIKIDATA', '"https://www.wikidata.org/wiki/Special:EntityData/"'
        }

        obf {
            applicationId "openfoodfacts.github.scrachx.openbeauty"
            resValue "string", "app_name", "OpenBeautyFacts"
            buildConfigField 'String', 'HOST', '"https://ssl-api.openbeautyfacts.org"'
            buildConfigField 'String', 'OFOTHERLINKAPP', '"org.openfoodfacts.scanner"'
            buildConfigField 'String', 'OFWEBSITE', '"https://world.openbeautyfacts.org/"'
            buildConfigField 'String', 'WIKIDATA', '"https://www.wikidata.org/wiki/Special:EntityData/"'
        }

        opff {
            applicationId "org.openpetfoodfacts.scanner"
            resValue "string", "app_name", "OpenPetFoodFacts"
            buildConfigField 'String', 'HOST', '"https://ssl-api.openpetfoodfacts.org"'
            buildConfigField 'String', 'OFOTHERLINKAPP', '"org.openfoodfacts.scanner"'
            buildConfigField 'String', 'OFWEBSITE', '"https://world.openpetfoodfacts.org/"'
            buildConfigField 'String', 'WIKIDATA', '"https://www.wikidata.org/wiki/Special:EntityData/"'
        }

        opf {
            applicationId "org.openproductsfacts.scanner"
            resValue "string", "app_name", "OpenProductsFacts"
            buildConfigField 'String', 'HOST', '"https://ssl-api.openproductsfacts.org"'
            buildConfigField 'String', 'OFOTHERLINKAPP', '"org.openfoodfacts.scanner"'
            buildConfigField 'String', 'OFWEBSITE', '"https://world.openproductsfacts.org/"'
            buildConfigField 'String', 'WIKIDATA', '"https://www.wikidata.org/wiki/Special:EntityData/"'
        }
    }

    dataBinding {
        enabled true
    }

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/*'
    }

    testOptions {
        // avoid "Method ... not mocked."
        unitTests.returnDefaultValues = true
        execution 'ANDROID_TEST_ORCHESTRATOR'
    }
}

ext {
    supportLibraryVersion = '28.0.0'
    daggerVersion = '2.13' //2.22.1
    retrofitVersion = '2.3.0' //2.5.0
    // gsonVersion = '2.8.5'
    jacksonVersion = '2.8.11' //2.9.9
    butterknifeVersion = '8.8.1' //10.1.0
    espressoVersion = '3.0.2'
}

dependencies {
//Android
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:exifinterface:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:preference-v14:$supportLibraryVersion"
    implementation "com.android.support:support-annotations:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3' //2.0.0-alpha4
    implementation 'com.android.support:multidex:1.0.3'

    //DI
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    compileOnly 'javax.annotation:jsr250-api:1.0' //OLD
    // compileOnly 'javax.annotation:javax.annotation-api:1.3.4'
    //https://github.com/javaee/javax.annotation
    //https://github.com/eclipse-ee4j/common-annotations-api

    //Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    //Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1' //3.14.1

    //Scheduling jobs
    //TODO: Deprecated; replace with WorkManager
    //https://developer.android.com/topic/libraries/architecture/workmanager/migrating-fb
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5' //0.8.6

    //Serialization/Deserialization
    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"

    //implementation 'io.rest-assured:json-path:3.3.0'

    //CSV Serialization/Deserialization
    implementation 'com.opencsv:opencsv:4.1' //4.5

    //Database
    implementation "org.greenrobot:greendao:$rootProject.greenDaoVersion"

    //Image Loading
    implementation 'com.squareup.picasso:picasso:2.5.2' //2.71828
    //implementation 'com.squareup.picasso3:picasso:3.0.0-SNAPSHOT'

    //Image from gallery or camera
    //DO NOT UPDATE : saveInAppExternalFilesDir() removed
    implementation 'com.github.jkwiecien:EasyImage:1.4.0' //3.0.1

    //Binding
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    //Barcode and QR Scanner
    implementation('com.journeyapps:zxing-android-embedded:3.6.0', {
        transitive = false
    })
    //For Android 14+ support zxing:core should be 3.3.0 or earlier
    implementation 'com.google.zxing:core:3.3.0' //3.3.3

    //HTML Dom
    implementation 'org.jsoup:jsoup:1.11.3'

    //Apache
    implementation 'org.apache.commons:commons-collections4:4.1' //4.3
    implementation 'commons-validator:commons-validator:1.6'


// UI Components

    //Custom toast
    implementation 'net.steamcrafted:load-toast:1.0.12'

    //ImageView with zooming
    implementation 'com.github.chrisbanes:PhotoView:2.1.3' //2.3.0

    //Material Dialog
    implementation('com.afollestad.material-dialogs:core:0.9.6.0', { //2.8.1 (kotlin)
        transitive = true
    })

    //Material Drawer
    implementation('com.mikepenz:materialdrawer:6.0.9@aar', { //6.1.2, 7.0.0-alpha2
        transitive = true
    })

    //Adapters
    //DO NOT UPDATE : RecyclerViewCacheUtil removed, needs rework
    implementation 'com.mikepenz:fastadapter-commons:2.1.0@aar' //3.3.1, 4.0.0-rc04
    implementation 'com.squareup.retrofit2:converter-scalars:2.1.0' //2.5.0

    //Floating Action Button
    implementation 'com.github.clans:fab:1.6.4'

    //Font Icons
    implementation 'com.mikepenz:iconics-core:3.0.4@aar' //4.0.0-rc01
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar' //3.0.1.3.original
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.4@aar'
    //2.2.0.5, 2.2.0.6-kotlin
    implementation 'com.mikepenz:fontawesome-typeface:5.0.6.0@aar' //5.3.1.1, 5.3.1.2-kotlin
    implementation('com.theartofdev.edmodo:android-image-cropper:2.7.0', { //2.8.0
        exclude group: 'com.android.support', module: 'appcompat-v7'
    })

    //Chips Input
    implementation 'com.hootsuite.android:nachos:1.1.2' //1.2.0


//Testing
    androidTestUtil 'com.android.support.test:orchestrator:1.0.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.27.0'
    testImplementation 'net.javacrumbs.json-unit:json-unit-fluent:1.27.0' //2.6.1

    // Required for instrumented tests
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-web:$espressoVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espressoVersion", {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    })
    resourcePlaceholders {
        files = ['xml/shortcuts.xml']
    }
}
